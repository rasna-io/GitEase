import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

import GitEase
import GitEase_Style
import GitEase_Style_Impl

import "qrc:/GitEase/Qml/Core/Scripts/GraphLayout.js"            as GraphLayout
import "qrc:/GitEase/Qml/Core/Scripts/GraphUtils.js"             as GraphUtils
import "qrc:/GitEase/Qml/Core/Scripts/CommitGraphDataLoader.js"  as DataLoader
import "qrc:/GitEase/Qml/Core/Scripts/CommitGraphFilter.js"      as Filter
import "qrc:/GitEase/Qml/Core/Scripts/CommitGraphNavigation.js"  as Navigation
import "qrc:/GitEase/Qml/Core/Scripts/CommitGraphMenuBuilder.js" as MenuBuilder

/*! ***********************************************************************************************
 * CommitGraphDock
 * ************************************************************************************************/

DetachablePanel {
    id: root

    /* Property Declarations
     * ****************************************************************************************/
    property AppModel               appModel                : null

    property BranchController       branchController        : null
    property RemoteController        remoteController       : null
    property UserAuthenticationPopup userAuthenticationPopup : null
    property MergeController        mergeController         : null
    property RebaseController       rebaseController        : null
    property CherryPickController   cherryPickController    : null
    property ConflictController     conflictController      : null
    property TagController          tagController           : null
    property StatusController       statusController        : null
    property CommitController       commitController        : null
    property RepositoryController   repositoryController    : null
    property NotificationController notificationController  : null
    property StashController        stashController         : null
    property ResetController        resetController         : null
    property TerminalController     terminalController      : null
    property LayoutController       layoutController        : null
    property var                    pluginController        : null
    property GitTreeController      gitTreeController       : null


    property AddBranchPopup          addBranchPopup         : null
    property AddTagPopup             addTagPopup            : null

    property bool   isForcePush: false
    property string pendingPushBranch: ""
    property string pendingMergeSource: ""
    property var    allCommits      : []
    property var    commits         : []
    property var    allCommitsHash  : ({})
    property string headHash        : ""

    property var selectedCommitHashes   : []
    property var selectedCommit         : null
    property int lastSelectedIndex      : -1

    property int hoveredIndex           : -1
    property string hoverSource         : ""   // "canvas" | "list" | "" - who last set hoveredIndex

    property string navigationRule  : "Author Email"
    property string filterText      : ""
    property string filterStartDate : ""
    property string filterEndDate   : ""
    property var    filterMode      : []
    property string branchFilter    : ""
    property string branchFilterHeadHash: ""

    property int    pageSize        : 200
    property int    commitsOffset   : 0
    property bool   isLoadingMore   : false
    property bool   hasMoreCommits  : true

    property var commitPositions    : ({})
    property int commitItemHeight   : 24
    property int commitItemSpacing  : 4
    property int columnSpacing      : 30

    property int commitsColGraphWidth       : root.activeItem.width * 0.08
    property int commitsColBranchTagWidth   : root.activeItem.width * 0.17
    property int commitsColMessageWidth     : root.activeItem.width * 0.6
    property int commitsColAuthorWidth      : root.activeItem.width * 0.08
    property int commitsColDateWidth        : root.activeItem.width * 0.17

    readonly property int minColGraphWidth      : 60
    readonly property int minColBranchTagWidth  : 80
    readonly property int minColMessageWidth    : 100
    readonly property int minColAuthorWidth     : 60
    readonly property int minColDateWidth       : 80

    readonly property bool hasAnyFilter         : Filter.hasAnyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.branchFilter)

    readonly property bool canRebaseSelected    : !!root.selectedCommit && !root.selectedCommit.isUncommitted &&
                                                   root.selectedCommit.hash !== root.headHash &&
                                                   !!root.branchController.getCurrentBranchName()

    /* Signals
     * ****************************************************************************************/
    signal commitClicked(string commitId)

    /* Object Properties
     * ****************************************************************************************/
    title: qsTr("Commit Graph")
    currentRepositoryName: root.appModel.currentRepository.name || ""

    /* Children
     * ****************************************************************************************/
    Shortcut {
        sequence: "Ctrl+R"
        context: Qt.WindowShortcut
        enabled: root.canRebaseSelected
        onActivated: root.executeRebase(root.selectedCommit.hash)
    }

    Rectangle {
        anchors.fill: parent
        color: Style.colors.primaryBackground

        GuideHoverTrigger {
            guideController: root.guideController
            guideId: "commit_context_menu_tutorial"
            guideName: "Commit Context Menu"
            guideIcon: Style.icons.ellipsisVertical
            guidePage: "graph"
            stepsFactory: function() {
                return [
                    {
                        targetProvider: function() { return commitsListView },
                        icon: Style.icons.ellipsisVertical,
                        title: "Right-Click for Actions",
                        description: "Right-click any commit to open a context menu — checkout, cherry-pick, create a branch or tag, rebase, push, and more. The available actions depend on the commit you click."
                    }
                ]
            }
        }

        EmptyStateView {
            title: "no commit to show"
            details: root.emptyStateDetailsText()
            visible: !root.commits || root.commits.length === 0
        }

        ColumnLayout {
            anchors.fill: parent
            spacing: 0

            Rectangle {
                Layout.fillWidth: true
                Layout.preferredHeight: (commits && commits.length > 0) ? 30 : 0
                visible: commits && commits.length > 0
                color: Style.colors.primaryBackground

                RowLayout {
                    anchors.fill: parent
                    spacing: 0

                    ResizableColumnHeader {
                        Layout.preferredWidth: root.commitsColGraphWidth
                        label: "Graph"
                        onResized: function(delta, startWidth) {
                            var newWidth = Math.max(root.minColGraphWidth, startWidth + delta)
                            var actualDelta = newWidth - root.commitsColGraphWidth
                            var nextWidth = root.commitsColBranchTagWidth - actualDelta
                            if (nextWidth < root.minColBranchTagWidth) {
                                nextWidth = root.minColBranchTagWidth
                                newWidth = root.commitsColGraphWidth + (root.commitsColBranchTagWidth - root.minColBranchTagWidth)
                            }
                            root.commitsColGraphWidth = newWidth
                            root.commitsColBranchTagWidth = nextWidth
                        }
                    }

                    ResizableColumnHeader {
                        Layout.preferredWidth: root.commitsColBranchTagWidth
                        label: "Branch/Tag"
                        onResized: function(delta, startWidth) {
                            var newWidth = Math.max(root.minColBranchTagWidth, startWidth + delta)
                            var actualDelta = newWidth - root.commitsColBranchTagWidth
                            var nextWidth = root.commitsColMessageWidth - actualDelta
                            if (nextWidth < root.minColMessageWidth) {
                                nextWidth = root.minColMessageWidth
                                newWidth = root.commitsColBranchTagWidth + (root.commitsColMessageWidth - root.minColMessageWidth)
                            }
                            root.commitsColBranchTagWidth = newWidth
                            root.commitsColMessageWidth = nextWidth
                        }
                    }

                    ResizableColumnHeader {
                        Layout.preferredWidth: root.commitsColMessageWidth
                        label: "Message"
                        onResized: function(delta, startWidth) {
                            var newWidth = Math.max(root.minColMessageWidth, startWidth + delta)
                            var actualDelta = newWidth - root.commitsColMessageWidth
                            var nextWidth = root.commitsColAuthorWidth - actualDelta
                            if (nextWidth < root.minColAuthorWidth) {
                                nextWidth = root.minColAuthorWidth
                                newWidth = root.commitsColMessageWidth + (root.commitsColAuthorWidth - root.minColAuthorWidth)
                            }
                            root.commitsColMessageWidth = newWidth
                            root.commitsColAuthorWidth = nextWidth
                        }
                    }

                    ResizableColumnHeader {
                        Layout.preferredWidth: root.commitsColAuthorWidth
                        label: "Author"
                        onResized: function(delta, startWidth) {
                            var newWidth = Math.max(root.minColAuthorWidth, startWidth + delta)
                            var actualDelta = newWidth - root.commitsColAuthorWidth
                            var nextWidth = root.commitsColDateWidth - actualDelta
                            if (nextWidth < root.minColDateWidth) {
                                nextWidth = root.minColDateWidth
                                newWidth = root.commitsColAuthorWidth + (root.commitsColDateWidth - root.minColDateWidth)
                            }
                            root.commitsColAuthorWidth = newWidth
                            root.commitsColDateWidth = nextWidth
                        }
                    }

                    Rectangle {
                        Layout.preferredWidth: root.commitsColDateWidth
                        Layout.fillHeight: true
                        color: Style.colors.primaryBackground
                        Label {
                            anchors.left: parent.left
                            anchors.verticalCenter: parent.verticalCenter
                            text: "Date"
                            color: Style.colors.foreground
                            font.pixelSize: Style.appFont.defaultPt
                            font.bold: true
                        }
                    }
                }
            }

            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 0

                Item {
                    Layout.preferredWidth: root.commitsColGraphWidth + root.commitsColBranchTagWidth
                    Layout.fillHeight: true

                    Flickable {
                        id: graphFlickable
                        Layout.preferredWidth: root.commitsColGraphWidth + root.commitsColBranchTagWidth
                        anchors.fill: parent
                        contentWidth: children[0].width
                        contentHeight: commitsListView.contentHeight

                        clip: true

                        interactive: true
                        flickableDirection: Flickable.VerticalFlick

                        property bool syncScroll: false

                        onContentYChanged: {
                            if (!syncScroll) {
                                commitsListView.syncScroll = true
                                commitsListView.contentY = contentY
                                commitsListView.syncScroll = false
                            }
                        }

                        CommitGraphCanvas {
                            id: graphCanvas
                            width: root.commitsColGraphWidth + root.commitsColBranchTagWidth
                            height: Math.max(commitsListView.contentHeight, graphFlickable.height)
                            commits: root.commits
                            commitPositions: root.commitPositions
                            columnSpacing: root.columnSpacing
                            commitItemHeight: root.commitItemHeight
                            commitItemSpacing: root.commitItemSpacing
                            selectedHashes: root.selectedCommitHashes
                            headHash: root.headHash
                            showAvatar: root.appModel?.appSettings?.generalSettings?.showAvatar ?? true
                            graphColumnWidth: root.commitsColGraphWidth
                            branchTagColumnWidth: root.commitsColBranchTagWidth
                            allCommitsHash: root.allCommitsHash
                            hoveredIndex: root.hoveredIndex
                            onHoverIndexChanged: function(index) {
                                if (index >= 0) {
                                    root.hoverSource = "canvas"
                                    root.hoveredIndex = index
                                } else if (root.hoverSource === "canvas") {
                                    root.hoveredIndex = -1
                                    root.hoverSource = ""
                                }
                            }
                            onInfiniteScroll: root.loadMoreCommits()
                        }
                    }
                }

                ListView {
                    id: commitsListView

                    Layout.fillWidth    : true
                    Layout.fillHeight   : true
                    Layout.minimumWidth : 100

                    model   : root.commits
                    clip    : true

                    property bool syncScroll: false

                    // Sync scroll position with graph
                    onContentYChanged: {
                        if (!syncScroll) {
                            graphFlickable.syncScroll = true
                            graphFlickable.contentY = contentY
                            graphFlickable.syncScroll = false
                        }

                        // Infinite scroll trigger (list side)
                        if (!root.isLoadingMore && root.hasMoreCommits) {
                            var remaining = commitsListView.contentHeight - (commitsListView.contentY + commitsListView.height)
                            if (remaining < 300) {
                                root.loadMoreCommits()
                            }
                        }
                    }

                    delegate: CommitListDelegate {
                        height: root.commitItemHeight + root.commitItemSpacing * 2

                        messageWidth: root.commitsColMessageWidth
                        authorWidth : root.commitsColAuthorWidth
                        dateWidth   : root.commitsColDateWidth

                        indicatorColor: graphCanvas.commitColor(modelData)

                        isSelected  : root.isCommitSelected(modelData.hash)
                        isHead      : modelData ? modelData.hash === root.headHash  : false
                        isStash     : modelData ? modelData.isStash === true        : false
                        parentRoot  : root.activeItem
                        hoveredIndex: root.hoveredIndex

                        onItemClicked: function(button, modifiers, idx, mouseX, mouseY) {
                            root.handleItemClick(modelData, button, modifiers, idx, mouseX, mouseY)
                        }

                        onItemDoubleClicked: function(button, modifiers, idx) {
                            root.handleItemDoubleClick(modelData, button, modifiers, idx)
                        }

                        onResetHeadOne: {
                            root.executeResetHead("HEAD~1", ResetController.ResetMode.Mixed)
                        }

                        onHoverEntered: function(_index) {
                            root.hoverSource = "list"
                            root.hoveredIndex = _index
                        }

                        onHoverExited: function(_index) {
                            if (root.hoverSource === "list" && root.hoveredIndex === _index) {
                                root.hoveredIndex = -1
                                root.hoverSource = ""
                            }
                        }
                    }
                }
            }
        }
    }

    ContextMenu {
        id: contextMenu
        width: 250
        parent : root.activeItem
    }


    Connections {
        id: pushAuthConnection
        target: userAuthenticationPopup
        enabled: false

        function onPasswordConfirm(password){
            let branchName = root.pendingPushBranch || branchController.getCurrentBranchName()
            if(branchName.length === 0){
                root.notificationController.error("Current branch name is invalid", "Branch Error", 5000)
            }else{
                remoteController.push(
                        "origin",
                        branchName,
                        password,
                        isForcePush)
                root.notificationController.info("Push operation started", "Push", 3000)
            }
            root.pendingPushBranch = ""
            pushAuthConnection.enabled = false
        }

        function onRejected() {
            root.pendingPushBranch = ""
            pushAuthConnection.enabled = false
        }
    }

    Connections {
        target: Style
        function onCurrentThemeChanged() {
            graphCanvas.requestPaint()
        }
    }

    Connections {
        target: root.repositoryController
        function onRepositorySelected() {
            root.reloadAll()
        }
    }

    Connections {
        target: root.addBranchPopup

        function onBranchCreatedSuccessfully() {
            root.selectedCommit         = null
            root.selectedCommitHashes   = []
            root.lastSelectedIndex      = -1
            root.reloadAll()
        }
    }

    Connections {
        target: root.addTagPopup
        function onTagCreatedSuccessfully() {
            root.selectedCommit = null
            root.reloadAll()
        }
    }

    Connections {
        target: root.tagController
        function onPushTagFinished(result) {
            if (result.success)
                notificationController.success("Tag created and pushed", "Success", 3000)
            else
                notificationController.warning("Tag created locally but failed to push", "Sync Warning", 5000);
        }
    }

    Connections {
        target: root.appModel?.appSettings?.generalSettings ?? null
        function onShowAvatarChanged() {
            graphCanvas.requestPaint()
        }

        function onShowStashNodesChanged() {
            root.refreshStashNodes()
        }
    }

    Connections {
        target: root.terminalController

        function onGitStateChanged() {
            root.reloadAll()
        }
    }

    onRepositoryControllerChanged: root.reloadAll()

    onStashControllerChanged: {
        if (root.allCommits.length)
            root.refreshStashNodes()
    }

    onAllCommitsChanged: {
        root.allCommitsHash = {}
        for (var i = 0; i < root.allCommits.length; i++) {
            var c = root.allCommits[i]
            if (c && c.hash) root.allCommitsHash[c.hash] = c.hash
        }
    }

    ConflictPopup {
        id: mergeConflictPopup
        hostItem                : root.activeItem
        currentOperation        : ConflictPopup.OperationType.Merge
        mergeController         : root.mergeController
        conflictController      : root.conflictController
        notificationController  : root.notificationController
        statusController        : root.statusController
        commitController        : root.commitController
        guideController         : root.guideController
        // onOperationCompleted    : reloadAll()        //TODO
    }

    MergeMethodPopup { id: mergeMethodPopup }

    Connections {
        target: mergeMethodPopup

        function onAccepted(noFF) {
            if (root.pendingMergeSource === "")
                return

            let source = root.pendingMergeSource
            root.pendingMergeSource = ""
            root.performMerge(source, noFF)
        }

        function onClosed() {
            root.pendingMergeSource = ""
        }
    }

    ConflictPopup {
        id: cherryPickConflictPopup
        hostItem                : root.activeItem
        currentOperation        : ConflictPopup.OperationType.CherryPick
        cherryPickController    : root.cherryPickController
        conflictController      : root.conflictController
        notificationController  : root.notificationController
        statusController        : root.statusController
        commitController        : root.commitController
        guideController         : root.guideController
        // onOperationCompleted    : reloadAll()        //TODO
    }

    CheckoutBranchSelectorPopup {
        id: checkoutBranchSelector
        property string commitHash: ""
        onBranchSelected: function(branchName) {
            root.handleCheckoutBranchOrCreate(branchName, checkoutBranchSelector.commitHash)
        }
    }

    CommitPlanPopup {
        id: commitPlanPopup
        hostItem: root.activeItem
        statusController: root.statusController
        commitController: root.commitController
        rebaseController: root.rebaseController
        conflictController: root.conflictController
        notificationController: root.notificationController
        layoutController: root.layoutController
        guideController: root.guideController
    }

    CommitFileBrowserPopup {
        id: commitFileBrowserPopup
        hostItem: root.activeItem
        gitTreeController       : root.gitTreeController
        repositoryController    : root.repositoryController
        notificationController  : root.notificationController
        statusController        : root.statusController
    }

    /* Functions
     * ****************************************************************************************/
    function emptyStateDetailsText() {
        if (!root.allCommits || root.allCommits.length === 0)
            return "This repository has no commits."

        // 2) Commits exist, but filter/search returned no matches
        if (!root.hasAnyFilter)
            return "No commits to show."

        var parts = []

        var needle = (root.filterText || "").trim()
        if (needle.length > 0) {
            var scope = (root.navigationRule === "Author") ? "author" : "message"
            parts.push(scope + " contains '" + needle + "'")
        }

        var start = (root.filterStartDate || "").trim()
        var end = (root.filterEndDate || "").trim()
        if (start.length > 0 || end.length > 0) {
            if (start.length > 0 && end.length > 0)
                parts.push("date between " + start + " and " + end)
            else if (start.length > 0)
                parts.push("date from " + start)
            else
                parts.push("date until " + end)
        }

        var branch = (root.branchFilter || "").trim()
        if (branch.length > 0)
            parts.push("branch is '" + branch + "'")

        if (parts.length === 0)
            return "No commits match your filter."

        return "No commits match: " + parts.join(", ")
    }

    function clearGraphCaches() {
        GraphUtils.clearBranchColorCache()
        GraphUtils.clearTagColorCache()
        GraphUtils.clearCategoryColorCache()
    }

    function layoutCommits(items) {
        return GraphLayout.calculateDAGPositions(items, root.columnSpacing, root.commitItemHeight, root.commitItemSpacing)
    }

    function applyFilter(text, startDate, endDate, modes) {
        if (text !== undefined)
            root.filterText = text

        if (startDate !== undefined)
            root.filterStartDate = startDate

        if (endDate !== undefined)
            root.filterEndDate = endDate

        if (modes !== undefined)
            root.filterMode = modes

        var result = Filter.applyFilter(
            root.allCommits,
            root.filterText,
            root.filterStartDate,
            root.filterEndDate,
            root.filterMode,
            root.selectedCommitHashes,
            root.branchFilter,
            root.branchFilterHeadHash
        )

        loadData(result.filtered)
        root.selectedCommitHashes = result.stillSelected
        if (!result.stillSelected.length) {
            root.selectedCommit = null
            root.lastSelectedIndex = -1
        }

        if (Filter.hasAnyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.branchFilter)) {
            ensureMinimumResults()
        }
    }

    function loadData(items) {
        var positions = layoutCommits(items)
        root.commitPositions = positions
        root.commits = items.slice(0)
    }

    function update() {
        graphCanvas.requestPaint()

    }

    function reloadAll() {
        if (!root.appModel || !root.appModel.currentRepository)
            return

        clearGraphCaches()
        commitsOffset   = 0
        hasMoreCommits  = true
        isLoadingMore   = false
        refreshBranchFilterHeadHash()

        root.headHash = root.statusController.getHeadHash()

        var commitRes = root.commitController.getCommits(pageSize, commitsOffset)
        if (!commitRes.success || !commitRes.data) return

        var page = commitRes.data
        var compiled = compilePage(page)

        var statusRes = root.statusController ? root.statusController.status() : null
        var uncommitted = DataLoader.createUncommittedNode(statusRes && statusRes.success ? statusRes.data : null, root.headHash)
        if (uncommitted) compiled.unshift(uncommitted)

        commitsOffset = page.length
        hasMoreCommits = (page.length === pageSize)

        root.allCommits = compiled.slice(0)
        root.applyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.filterMode)
    }

    /*!
     * Ensures that filtered results meet minimum threshold by loading more pages if needed.
     * Automatically loads additional pages until we have at least pageSize results or no more commits.
     */
    function ensureMinimumResults() {
        if (!Filter.hasAnyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.branchFilter))
            return

        if ((root.commits ? root.commits.length : 0) >= pageSize)
            return

        if (!hasMoreCommits || isLoadingMore)
            return

        loadMoreCommits()
    }

    /*!
     * Loads additional commits specifically for filter scenarios.
     * Continues loading until filtered results reach pageSize or no more commits available.
     */
    function loadMoreCommits() {
        if (isLoadingMore || !hasMoreCommits)
            return

        var currentContentY = commitsListView ? commitsListView.contentY : 0
        isLoadingMore = true

        var commitRes = root.commitController.getCommits(pageSize, commitsOffset)
        if (!commitRes.success || !commitRes.data) {
            isLoadingMore = false
            return
        }

        var page = commitRes.data
        if (!page.length) {
            hasMoreCommits = false
            isLoadingMore = false
            return
        }

        var compiled = compilePage(page)
        var withoutStashes = root.allCommits.filter(function(c) { return !c.isStash })
        root.allCommits = withoutStashes.concat(compiled)

        commitsOffset += page.length
        hasMoreCommits = (page.length === pageSize)

        root.applyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.filterMode)
        if (commitsListView) commitsListView.contentY = currentContentY

        isLoadingMore = false
    }

    function compilePage(page) {
        return DataLoader.compileGraphCommits(
            page,
            root.branchController ? root.branchController.getBranches() : [],
            getStashes(),
            getTags(),
            root.appModel && root.appModel.appSettings ? root.appModel.appSettings.generalSettings : null
        )
    }

    /**
     * Refreshes stash nodes in the existing commit list without re-fetching all commits.
     * Called when stashController becomes available after the initial load, or when
     * stashes change (save/pop/drop).
     */
    function refreshStashNodes() {
        if (!root.allCommits.length)
            return

        var baseCommits = root.allCommits.filter(function(c) { return !c.isStash })
        root.allCommits = DataLoader.compileGraphCommits(
            baseCommits,
            root.branchController ? root.branchController.getBranches() : [],
            getStashes(),
            getTags(),
            root.appModel && root.appModel.appSettings ? root.appModel.appSettings.generalSettings : null
        )

        root.applyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.filterMode)
    }

    function getTags() {
        if (!root.tagController)
            return
        var tagRes = root.tagController.list()
        if (!tagRes.success || !tagRes.data)
            return []

        return tagRes.data
    }

    function getStashes() {
        if (!root.stashController)
            return
        var stashRes = root.stashController.list()
        if (!stashRes.success || !stashRes.data)
            return []

        return stashRes.data
    }

    function isCommitSelected(hash) {
        return root.selectedCommitHashes && root.selectedCommitHashes.indexOf(hash) !== -1
    }

    function setSingleSelection(commitData, index) {
        root.selectedCommitHashes = [commitData.hash]
        root.lastSelectedIndex = index
    }

    function selectedCommitsInOrder() {
        var selected = []
        if (!root.commits || !root.selectedCommitHashes) return selected
        for (var i = root.commits.length - 1; i >= 0; i--) {
            var c = root.commits[i]
            if (c && c.hash && isCommitSelected(c.hash)) selected.push(c)
        }
        return selected
    }

    function handleItemClick(data, button, modifiers, idx, mouseX, mouseY) {
        if (!data)
            return

        root.selectedCommit = data
        root.commitClicked(data.isUncommitted ? "__uncommitted__" : data.hash)

        if (button === Qt.RightButton) {
            if (data.isUncommitted){
                root.selectedCommit = data;
                root.commitClicked("__uncommitted__");
                return;
            }

            if (!isCommitSelected(data.hash))
                setSingleSelection(data, idx)

            var state = getMenuState(data)
            var pluginItems = root.pluginController?.pluginManager
                ? root.pluginController.pluginManager.pluginContextMenuItems(
                      "commit", { hash: state.fullHash, branch: state.currentBranch })
                : []
            var rawMenu = MenuBuilder.buildMenu(state, pluginItems)

            contextMenu.menuModel = buildContextMenuModel(rawMenu)

            contextMenu.x = mouseX
            contextMenu.y = mouseY
            contextMenu.open()
            return
        }

        if (data.isUncommitted) return
        var selection = Navigation.applySelection(data, idx, modifiers, root.selectedCommitHashes, root.lastSelectedIndex, root.commits)
        if (selection) {
            root.selectedCommitHashes = selection.hashes
            root.lastSelectedIndex = selection.lastIndex
        }
    }

    function getMenuState(commitData) {
        var branches        = commitData.branchNames || []
        var shortHash       = commitData.shortHash || commitData.hash.substring(0, 7)
        var isHead          = commitData.hash === root.headHash
        var currentBranch   = root.branchController.getCurrentBranchName()
        var mergeable       = branches.filter(function(b) { return b !== currentBranch && !b.startsWith("origin/") })

        return {
            currentBranch       : currentBranch,
            isHead              : isHead,
            shortHash           : shortHash,
            fullHash            : commitData.hash,
            commitMessage       : commitData.message || "",
            commitDate          : commitData.authorDate || "",
            pushEnabled         : !remoteController.pushInProgress && isHead,
            branchNames         : branches,
            isStash             : commitData.isStash || false,
            canCherryPick       : !commitData.isStash && !isHead,
            canRebase           : !!currentBranch && !isHead,
            numSelected         : selectedCommitHashesInOrder().length,
            cherryPickEnabled   : !selectionHasStash() && !selectionHasHead(),
            hasMergeableBranches: mergeable.length > 0,
            mergeableBranches   : mergeable
        }
    }

    function selectedCommitHashesInOrder() {
        var selected = selectedCommitsInOrder()
        var hashes = []
        for (var i = 0; i < selected.length; i++) hashes.push(selected[i].hash)
        return hashes
    }

    function selectionHasStash() {
        var selected = selectedCommitsInOrder()
        for (var i = 0; i < selected.length; i++) if (selected[i].isStash) return true
        return false
    }

    function selectionHasHead() {
        var selected = selectedCommitsInOrder()
        for (var i = 0; i < selected.length; i++) if (selected[i].hash === root.headHash) return true
        return false
    }

    function buildContextMenuModel(raw) {
        return raw.map(function(item) {

            if (item.separator)
                return { separator: true }

            var result = {
                text    : item.text,
                icon    : resolveMenuIcon(item.icon),
                enabled : item.enabled !== false,
                hasCheckBox: item.hasCheckBox,
                checkBoxText: item.checkBoxText,
                shortcut: item.shortcut
            }

            if (item.subItems) {
                result.subItems = buildContextMenuModel(item.subItems)
            } else {
                result.action = function(checked) {
                    root.executeMenuAction(item, checked)
                }
            }

            return result
        })
    }

    function resolveMenuIcon(iconName) {
        return Style.icons[iconName]
    }

    function executeMenuAction(item, checked) {
        switch (item.action) {

        case "checkoutBranch":
            executeCheckoutBranch(item.payload.branch)
            break

        case "checkoutCommit":
            executeCheckoutCommit(item.payload.hash)
            break

        case "push":
            executePush(item.payload.branch, checked)
            break

        case "newBranch":
            executeNewBranch(item.payload.hash)
            break

        case "newTag":
            executeNewTag(item.payload.hash)
            break

        case "browseFiles":
            browseFilesRequested(item.payload.hash, item.payload.message, item.payload.date)
            break

        case "mergeBranch":
            executeMergeBranch(item.payload.source, item.payload.target)
            break

        case "rebase":
            executeRebase(item.payload.hash)
            break
        case "cherryPickSelected":
            executeCherryPickSelected()
            break

        case "cherryPickSingle":
            executeCherryPickSingle(item.payload.hash)
            break

        case "resetSoft":
            executeResetHead(item.payload.hash, ResetController.ResetMode.Soft)
            break

        case "resetMixed":
            executeResetHead(item.payload.hash, ResetController.ResetMode.Mixed)
            break

        case "resetHard":
            executeResetHead(item.payload.hash, ResetController.ResetMode.Hard)
            break

        case "pluginAction":
            if (root.pluginController?.pluginManager)
                root.pluginController.pluginManager.executeContextMenuAction(
                    item.payload.pluginId,
                    item.payload.itemId,
                    "commit",
                    { hash: item.payload.hash })
            break
        }
    }

    function executeCheckoutBranch(branchName) {
        handleContextResponse(root.branchController.checkoutBranch(branchName), "Checked out branch " + branchName)
    }

    function executeCheckoutCommit(commitHash) {
        handleContextResponse(root.branchController.checkoutCommit(commitHash), "Checked out commit " + commitHash.substring(0, 7))
    }

    function executePush(branchName, force) {
        isForcePush = force
        let urlRes = remoteController.getRemoteUrl("origin")
        if (!urlRes.success) {
            root.notificationController.error(urlRes.errorMessage || "Failed to get remote URL", `${isForcePush ? "Force" : ""} Push Error`, 5000)
            return
        }
        let protocol = repositoryController.detectGitProtocol(urlRes.data.url)
        switch (protocol) {
        case RepositoryController.GitProtocol.SSH: {
            remoteController.push("origin", branchName, isForcePush)
            root.notificationController.info("Push operation started", "Push", 3000)
            break
        }

        // Fall-through: both HTTP/HTTPS require auth popup
        case RepositoryController.GitProtocol.HTTPS:
        case RepositoryController.GitProtocol.HTTP:
            root.pendingPushBranch = branchName
            pushAuthConnection.enabled = true
            root.openPopup(userAuthenticationPopup)
            break
        default:
            root.notificationController.error("Unsupported protocol", `${isForcePush ? "Force" : ""} Push Error`, 5000)
        }
    }

    function executeShowOnlyBranch(branchName) {
        root.branchFilter = branchName || ""
        refreshBranchFilterHeadHash()
        root.applyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.filterMode)
    }

    function executeShowAllBranches() {
        root.branchFilter = ""
        root.branchFilterHeadHash = ""
        root.applyFilter(root.filterText, root.filterStartDate, root.filterEndDate, root.filterMode)
    }

    function refreshBranchFilterHeadHash() {
        root.branchFilterHeadHash = findBranchHeadHash(root.branchFilter)
    }

    function findBranchHeadHash(branchName) {
        if (!branchName || !root.branchController)
            return ""

        var branches = root.branchController.getBranches()
        if (!branches)
            return ""

        for (var i = 0; i < branches.length; i++) {
            var branch = branches[i]
            if (branch && branch.name === branchName)
                return branch.targetHash || ""
        }

        return ""
    }

    function executeNewBranch(commitHash) {
        if (!root.addBranchPopup)
            return

        root.addBranchPopup.branchController    = root.branchController
        root.addBranchPopup.targetHash          = commitHash
        root.openPopup(root.addBranchPopup)
    }

    function executeNewTag(commitHash) {
        if (!root.addTagPopup)
            return

        root.addTagPopup.tagController  = root.tagController || null
        root.addTagPopup.targetHash     = commitHash
        root.openPopup(root.addTagPopup)
    }

    function browseFilesRequested(commitHash, commitMessage, commitDate) {
        commitFileBrowserPopup.openForCommit(commitHash, commitMessage, commitDate)
    }

    function executeMergeBranch(source, target) {
        mergeMethodPopup.sourceBranch = source
        mergeMethodPopup.targetBranch = target
        root.pendingMergeSource = source

        // mergeMethodPopup is declared in this panel's content, so it follows the panel by itself.
        mergeMethodPopup.open()
    }

    function performMerge(source, noFF) {
        var res = root.mergeController.mergeBranchIntoCurrent(source, noFF)

        if (root.mergeController.isMergeInProgress() && root.mergeController.hasMergeConflicts()) {
            root.showConflictWindow(mergeConflictPopup)
            root.notificationController.warning("Merge conflicts detected.", "Merge", 4000)
            root.reloadAll()
        } else {
            handleGitControllerResult(res, "Merge completed", mergeConflictPopup, "Merge")
        }
    }

    function executeRebase(commitHash) {

        commitPlanPopup.show()

        rebaseController.startPreviewRebasePlan("", commitHash, "")
    }

    function executeCherryPickSelected() {
        var hashes  = selectedCommitHashesInOrder();
        var res     = cherryPickController.cherryPickCommits(hashes);

        handleGitControllerResult(res, "Cherry-pick completed", cherryPickConflictPopup, "Cherry-Pick");
    }

    function executeCherryPickSingle(commitHash) {
        var res = cherryPickController.cherryPickCommit(commitHash);

        handleGitControllerResult(res, "Cherry-pick completed", cherryPickConflictPopup, "Cherry-Pick");
    }

    function executeResetHead(commitHash, mode) {
        let res = root.resetController.resetHead(commitHash, mode)

        if (res.success) {
            root.notificationController.success("Reset completed successfully", "Reset", 3000)
            root.reloadAll()
        } else {
            root.notificationController.error(res.errorMessage, "Reset", 5000)
        }
    }

    function showConflictWindow(conflictPopup) {
        conflictPopup.ontoRef = root.branchController?.getCurrentBranchName() ?? ""
        conflictPopup.show()
    }

    function handleGitControllerResult(res, successMsg, conflictPopup, commandName) {
        if (res && res.success) {
            notificationController.success(successMsg, commandName, 3000)
        }
        else if (res && res.data && (res.data.status === "conflict" || res.data.hasConflicts)) {
            root.showConflictWindow(conflictPopup);
            notificationController.warning(commandName + " conflicts detected.", commandName, 4000);
        }
        else {
            notificationController.error(res ? res.errorMessage : commandName + " failed", commandName, 5000);
        }

        root.reloadAll();
    }

    function handleItemDoubleClick(data, button, modifiers, idx) {
        if (button !== Qt.LeftButton || !data)
            return

        if (data.isUncommitted || data.isStash || data.hash === root.headHash)
            return

        let isHead      = data.isHead || false
        var branches    = data.branchNames || []
        var shortHash   = data.shortHash || data.hash.substring(0, 7)

        if (!isHead) {
            var checkoutCommitRes = root.branchController.checkoutCommit(data.hash)
            handleContextResponse(checkoutCommitRes, "Checked out commit " + shortHash)
            return
        }

        var allBranches = root.branchController.getBranches()
        var localNames  = {}
        for (var i = 0; i < allBranches.length; i++) {
            var b = allBranches[i]
            if (b && b.isLocal) localNames[b.name] = true
        }

        var seen = {}
        var deduped = []
        for (var j = 0; j < branches.length; j++) {
            var name = branches[j]
            var base = name.replace(/^[^/]+\//, "")
            if (!seen[base]) {
                seen[base] = true
                deduped.push(localNames[base] ? base : name)
            }
        }

        if (deduped.length === 1) {
            handleCheckoutBranchOrCreate(deduped[0], data.hash)
        } else {
            checkoutBranchSelector.commitHash = data.hash
            checkoutBranchSelector.branches = deduped
            checkoutBranchSelector.open()
        }
    }

    function handleCheckoutBranchOrCreate(branchName, commitHash) {
        var allBranches = root.branchController.getBranches()
        var localNames  = {}
        for (var i = 0; i < allBranches.length; i++) {
            var b = allBranches[i]
            if (b && b.isLocal) localNames[b.name] = true
        }

        if (localNames[branchName]) {
            handleContextResponse(root.branchController.checkoutBranch(branchName), "Checked out branch " + branchName)
            return
        }

        var localName = branchName.replace(/^[^/]+\//, "")
        var createRes = root.branchController.createBranch(commitHash, localName)
        if (!createRes.success) {
            handleContextResponse(createRes, "")
            return
        }

        handleContextResponse(root.branchController.checkoutBranch(localName), "Created and checked out branch " + localName)
    }

    function handleContextResponse(res, successMsg) {
        if (res && res.success)
            root.notificationController.success(successMsg, "Checkout", 3000)

        else
            root.notificationController.error(res ? (res.errorMessage || "Operation failed") : "Operation failed", "Operation Error", 5000)

        root.selectedCommit = null
        root.selectedCommitHashes = []
        root.lastSelectedIndex = -1
        root.reloadAll()
    }

    function selectNext(rule) {
        var idx     = Navigation.selectedIndex(root.commits, root.selectedCommit)
        var result  = Navigation.selectNext(root.commits, root.selectedCommit, idx, root.navigationRule, rule)

        if (!result)
            return

        if (rule !== undefined)
            root.navigationRule = rule

        setSingleSelection(result.selected, result.index)
        root.selectedCommit = result.selected
        root.commitClicked(result.selected.hash)

        if (result.scroll)
            commitsListView.positionViewAtIndex(result.index, ListView.Contain)
    }

    function selectPrevious(rule) {
        var idx     = Navigation.selectedIndex(root.commits, root.selectedCommit)
        var result  = Navigation.selectPrevious(root.commits, root.selectedCommit, idx, root.navigationRule, rule)

        if (!result)
            return

        if (rule !== undefined)
            root.navigationRule = rule

        setSingleSelection(result.selected, result.index)
        root.selectedCommit = result.selected
        root.commitClicked(result.selected.hash)

        if (result.scroll)
            commitsListView.positionViewAtIndex(result.index, ListView.Contain)
    }
}
