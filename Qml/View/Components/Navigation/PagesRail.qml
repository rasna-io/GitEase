import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Effects

import GitEase_Style
import GitEase_Style_Impl
import GitEase

/*! ***********************************************************************************************
 * PagesRail
 * Sidebar component show pages
 * ************************************************************************************************/
Rectangle {
    id: root

    /* Property Declarations
     * ****************************************************************************************/
    property alias model: rpt.model

    property var   currentId

    property GuideController   guideController: null

    property bool   useAccentIndicator: false

    /* Signals
     * ****************************************************************************************/

    signal clicked(var modelData);

    /* Guide
     * ****************************************************************************************/
    GuideHoverTrigger {
        guideController: root.guideController
        guideId: "pages_rail_tutorial"
        guideName: "Pages Rail"
        guideIcon: Style.icons.list
        stepsFactory: function() {
            return [
                {
                    targetProvider: function() { return root },
                    icon: Style.icons.list,
                    title: "Pages",
                    description: "Switch between the app's pages — like Committing and Graph View — from this list. Hover the rail to see full names, or click a page to jump to it."
                }
            ]
        }
    }

    /* Children
     * ****************************************************************************************/
    ColumnLayout {
        anchors.fill: parent
        anchors.leftMargin: root.useAccentIndicator ? 0 : Style.dp(4)
        anchors.rightMargin: root.useAccentIndicator ? 0 : Style.dp(4)
        anchors.topMargin: 12

        // Pages list
        Flickable {
            Layout.fillWidth: true
            Layout.fillHeight: true
            contentHeight: pagesColumn.height
            clip: true

            Column {
                id: pagesColumn
                width: parent.width

                Repeater {
                    id: rpt

                    Column {
                        id: rowWrapper
                        width: parent.width
                        spacing: 0

                        // A group boundary is either the legacy plugin-section break, or an
                        // explicit groupStart flag (accent-indicator variant only).
                        readonly property bool isGroupBoundary:
                            ((modelData?.isPlugin === true) &&
                             (index === 0 || !(rpt.model[index - 1]?.isPlugin === true)))
                            || (root.useAccentIndicator && modelData?.groupStart === true && index > 0)

                        // Spacer above separator
                        Item {
                            width: parent.width
                            height: 6
                            visible: rowWrapper.isGroupBoundary
                        }

                        // Separator before a new group
                        Rectangle {
                            width: root.useAccentIndicator ? parent.width : parent.width - 16
                            anchors.horizontalCenter: parent.horizontalCenter
                            height: 1
                            color: Style.colors.primaryBorder
                            visible: rowWrapper.isGroupBoundary
                        }

                        // Spacer above separator
                        Item {
                            width: parent.width
                            height: 6
                            visible: rowWrapper.isGroupBoundary
                        }

                        Rectangle {
                            id: item
                            width: parent.width
                            height: Style.dp(30)
                            radius: root.useAccentIndicator ? 0 : Style.dp(6)

                            property bool isSelected: (modelData)
                                                      ? (modelData.pageId === root.currentId)
                                                      : false

                            readonly property color activeColor:   root.useAccentIndicator ? Style.colors.accent : "#60A5FA"
                            readonly property color inactiveColor: root.useAccentIndicator ? Style.colors.mutedText : "#363650"

                            color: item.isSelected ? "#1F3B82F6" : "transparent"

                            // Active indicator bar (accent-indicator variant only)
                            Rectangle {
                                anchors.left: parent.left
                                anchors.top: parent.top
                                anchors.bottom: parent.bottom
                                width: 3
                                radius: 1.5
                                color: Style.colors.accent
                                visible: root.useAccentIndicator && item.isSelected
                            }

                            RowLayout {
                                anchors {
                                    fill: parent
                                    leftMargin: root.useAccentIndicator ? Style.dp(16) : Style.dp(12)
                                    rightMargin: Style.dp(8)
                                }
                                spacing: root.useAccentIndicator ? 10 : 8

                                // Icon
                                Rectangle {
                                    Layout.alignment: Qt.AlignVCenter | Qt.AlignHCenter
                                    width: Style.dp(14)
                                    height: Style.dp(14)
                                    color: "transparent"

                                    Text {
                                        anchors.centerIn: parent
                                        text: (modelData && modelData.icon && modelData.icon.length)
                                              ? modelData.icon
                                              : Style.icons.download
                                        font.pixelSize: Style.appFont.h3Pt
                                        font.family: Style.fontTypes.font6Pro
                                        font.weight: (root.useAccentIndicator && item.isSelected) ? 600 : 500
                                        color: item.isSelected ? item.activeColor : item.inactiveColor
                                        horizontalAlignment: Text.AlignHCenter
                                        verticalAlignment: Text.AlignVCenter
                                    }
                                }

                                // Title
                                Text {
                                    Layout.fillWidth: true
                                    Layout.alignment: Qt.AlignVCenter
                                    text: (modelData && modelData.title) ? modelData.title : ""
                                    font.pixelSize: Style.appFont.h3Pt
                                    font.family: Style.fontTypes.inter
                                    font.weight: (root.useAccentIndicator && item.isSelected) ? Font.DemiBold : Font.Normal
                                    color: item.isSelected ? item.activeColor : item.inactiveColor
                                    elide: Text.ElideRight
                                }

                                // Badge
                                Rectangle {
                                    Layout.alignment: Qt.AlignVCenter
                                    visible: (modelData?.badgeCount ?? -1) >= 0
                                    implicitHeight: 16
                                    implicitWidth: Math.max(implicitHeight, badgeLabel.implicitWidth + 8)
                                    radius: implicitHeight / 2
                                    color: modelData?.badgeColor ?? "#3B82F6"

                                    Label {
                                        id: badgeLabel
                                        anchors.centerIn: parent
                                        text: modelData?.badgeCount ?? ""
                                        color: "white"
                                        font.family: Style.fontTypes.inter
                                        font.pixelSize: Style.appFont.smallPt
                                        font.bold: true
                                    }
                                }
                            }

                            MouseArea {
                                anchors.fill: parent
                                cursorShape: Qt.PointingHandCursor
                                hoverEnabled: true

                                onClicked: root.clicked(modelData)
                            }
                        }
                    }
                }
            }
        }
    }
}
